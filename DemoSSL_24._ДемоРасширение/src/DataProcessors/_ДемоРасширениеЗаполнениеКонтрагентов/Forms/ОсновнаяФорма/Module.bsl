#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура Подключаемый_ЗаполнитьПолноеНаименование(МассивКонтрагентов, ПараметрыВыполнения) Экспорт
	Объект = ПараметрыВыполнения.Форма.Объект;
	НовоеЗначение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Команда: %1;
		|Дата изменения: %2.'"),
		ПараметрыВыполнения.ОписаниеКоманды.Представление,
		Формат(ОбщегоНазначенияКлиент.ДатаСеанса(), "ДФ=yyyy-MM-dd"));
	
	Если Не ПустаяСтрока(Объект.НаименованиеПолное) Тогда
		Если Объект.НаименованиеПолное = НовоеЗначение Тогда
			ПоказатьОповещениеПользователя(, , НСтр("ru = 'Полное наименование уже заполнено.'"), БиблиотекаКартинок.Информация32);
			Возврат;
		Иначе
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Объект ""%1"" не обработан: реквизит ""НаименованиеПолное"" не пустой!'"),
				Строка(Объект.Наименование));
		КонецЕсли;
	КонецЕсли;
	
	Объект.НаименованиеПолное = НовоеЗначение;
	
	ПоказатьОповещениеПользователя(, , НСтр("ru = 'Полное наименование успешно заполнено.'"), БиблиотекаКартинок.Успешно32);
	
	ПараметрыВыполнения.Форма.Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьВсе(МассивКонтрагентов, ПараметрыВыполнения) Экспорт
	ДлительнаяОперация = ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор, МассивКонтрагентов, ПараметрыВыполнения.ОписаниеКоманды);
	
	НастройкиОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	НастройкиОжидания.ВыводитьОкноОжидания = Истина;
	НастройкиОжидания.ТекстСообщения = НСтр("ru = 'Идет комплексное заполнение...'");
	
	Обработчик = Новый ОписаниеОповещения("ДлительнаяОперацияЗавершениеКлиент", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, НастройкиОжидания);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДлительнаяОперацияЗапускСервер(УникальныйИдентификатор, МассивКонтрагентов, ОписаниеКоманды)
	ИмяМетода = "Обработки._ДемоРасширениеЗаполнениеКонтрагентов.ВыполнитьКомандуВФоне";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПараметрКоманды", МассивКонтрагентов);
	ПараметрыПроцедуры.Вставить("ОписаниеКоманды", ОписаниеКоманды);
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Демо: Заполнение контрагентов'");
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыПроцедуры, НастройкиЗапуска);
КонецФункции

&НаКлиенте
Процедура ДлительнаяОперацияЗавершениеКлиент(Операция, ДополнительныеПараметры) Экспорт
	Если Операция = Неопределено Тогда
		Возврат; // Задание отменено.
	КонецЕсли;
	Обработчик = Новый ОписаниеОповещения("ДлительнаяОперацияПослеВыводаРезультата", ЭтотОбъект);
	Если Операция.Статус = "Выполнено" Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Контрагенты успешно заполнены'"),,, БиблиотекаКартинок.Успешно32);
		ВыполнитьОбработкуОповещения(Обработчик);
	Иначе
		ВызватьИсключение Операция.КраткоеПредставлениеОшибки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДлительнаяОперацияПослеВыводаРезультата(ПустойПараметр, Результат) Экспорт
	ОповеститьОбИзменении(Тип("СправочникСсылка._ДемоКонтрагенты"));
	Если ТипЗнч(ВладелецФормы) = Тип("УправляемаяФорма") И ВладелецФормы.ИмяФормы = "Справочник._ДемоКонтрагенты.Форма.ФормаЭлемента" Тогда
		ВладелецФормы.Прочитать();
	КонецЕсли;
	Если Открыта() Тогда
		Закрыть(Истина);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
