#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - УправляемаяФорма, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - Структура - см. возвращаемое значение
//       ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.ФормироватьСразу = Истина;
	Настройки.ФормироватьСразу = Истина;
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - УправляемаяФорма - Форма отчета.
//   Отказ - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Передается из параметров обработчика "как есть".
//
// См. также:
//   "УправляемаяФорма.ПриСозданииНаСервере" в синтакс-помощнике.
//   ОтчетыПереопределяемый.ПриСозданииНаСервере().
//
// Пример 1 - Добавление команды с обработчиком в ОтчетыКлиентПереопределяемый.ОбработчикКоманды:
//	Команда = Форма.Команды.Добавить("МояОсобеннаяКоманда");
//	Команда.Действие  = "Подключаемый_Команда";
//	Команда.Заголовок = НСтр("ru = 'Моя команда...'");
//	
//	Кнопка = Форма.Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Форма.Элементы.ГруппаПрограммныйИнтерфейс);
//	Кнопка.ИмяКоманды = Команда.Имя;
//	
//	Форма.ПостоянныеКоманды.Добавить(КомандаСоздать.Имя);
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	МассивСсылок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Форма.Параметры, "ПараметрКоманды");
	Если ТипЗнч(МассивСсылок) = Тип("Массив") И МассивСсылок.Количество() > 0 Тогда
		Если ТипЗнч(МассивСсылок[0]) = Тип("СправочникСсылка._ДемоМестаХранения") Тогда
			МассивСкладов = МассивСсылок;
			МассивНоменклатуры = Новый Массив;
		Иначе
			МассивСкладов = Новый Массив;
			МассивНоменклатуры = Новый Массив;
			ЗначенияРеквизитовОбъектов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок, "МестоХранения, Товары");
			Для Каждого Ссылка Из МассивСсылок Цикл
				Значения = ЗначенияРеквизитовОбъектов.Получить(Ссылка);
				Если МассивСкладов.Найти(Значения.МестоХранения) = Неопределено Тогда
					МассивСкладов.Добавить(Значения.МестоХранения);
				КонецЕсли;
				ТаблицаТовары = Значения.Товары.Выгрузить();
				Для Каждого СтрокаТаблицы Из ТаблицаТовары Цикл
					Если МассивНоменклатуры.Найти(СтрокаТаблицы.Номенклатура) = Неопределено Тогда
						МассивНоменклатуры.Добавить(СтрокаТаблицы.Номенклатура);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Форма.ФормаПараметры.Отбор.Вставить("МестоХранения", МассивСкладов);
		Если МассивНоменклатуры.Количество() > 0 Тогда
			Форма.ФормаПараметры.Отбор.Вставить("Номенклатура", МассивНоменклатуры);
		КонецЕсли;
	КонецЕсли;
	
	Команда = Форма.Команды.Добавить("_ДемоКоманда");
	Команда.Действие  = "Подключаемый_Команда";
	Команда.Заголовок = НСтр("ru = 'Изменить табличный документ'");
	Команда.Картинка  = БиблиотекаКартинок.Изменить;
	
	ОтчетыСервер.ВывестиКоманду(Форма, Команда, "РаботаСТабличнымДокументом");
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли